open import Mvu;
open import MvuHTML;
open import MvuAttrs;
open import MvuEvents;
open import MvuCommands;
import Common;

var db = database "links";
var users = table "users" with (email : String, name : String, password : String) from db;

typename Model = (email: String, password:String, name:String, signUpSuccess:Bool);
typename Message = [| TrySignUp | DeliverResult | UpdateEmail:String | UpdateName:String | UpdatePassword:String |];

sig initialModel : Model
var initialModel = (email = "", password = "", name = "" ,signUpSuccess = false);

sig insertUser : (String, String, String) ~> ()
fun insertUser(enteredEmail,enteredName,enteredPassword) server {
    insert users values (email, name, password) [(email = enteredEmail, name = enteredName, password = enteredPassword)]
}

sig view : (Model) ~> MvuHTML.HTML(Message)
fun view(model) {

  Common.viewHeader() +*
  form(a0,
    input(type("email") +@ placeholder("Email") +@ name("email") +@ onInput(fun(str) { UpdateEmail(str) }), h0) +*
    input(type("text") +@ placeholder("Name") +@ name("name") +@ onInput(fun(str) { UpdateName(str) }), h0) +*
    input(type("password") +@ placeholder("Password") +@ name("password") +@ onInput(fun(str) { UpdatePassword(str) }), h0) +*
    button(onClick(fun() { TrySignUp }), textNode("Sign Up"))
    ) +*

    a(href("/login"), textNode("Log In"))
}

sig updt : (Message, Model) ~%~> (Model, MvuCommands.Command(Message))
fun updt(msg, model) {
  switch(msg) {
    case TrySignUp ->
      var cmd =
        MvuCommands.spawnProc(fun() { DeliverResult(insertUser(model.email, model.name, model.password)) });
      ((model with signUpSuccess = false), cmd)
    case DeliverResult -> ((model with signUpSuccess = true), MvuCommands.empty)
    case UpdateEmail(txt) -> ((model with email = txt), MvuCommands.empty)
    case UpdateName(txt) -> ((model with name = txt), MvuCommands.empty)
    case UpdatePassword(txt) -> ((model with password = txt), MvuCommands.empty)
  }
}

fun mainPage() {
  var loggedIn = getCookie("currentUser") <> "";
    if (loggedIn) {
        redirect("/")
    } else {
        ()
    };

  Mvu.runCmd("placeholder", initialModel, view, updt, MvuCommands.empty);
  Common.template()
}