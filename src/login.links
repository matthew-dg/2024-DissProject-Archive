open import Mvu;
open import MvuHTML;
open import MvuAttrs;
open import MvuEvents;
open import MvuCommands;
import HomePage;

typename Model = (email: String, password: String, loginSuccess:Bool);

typename Message = [| DeliverResult:String | TryLogin | UpdateEmail:String | UpdatePassword:String |];

sig initialModel : Model
var initialModel = (email = "", password = "", loginSuccess = false);

#Database
var db = database "links";
var users = table "users" with (email : String, password : String ,name : String, userid : Int, affiliation : String, pcmember : Bool, admin : Bool) from db;

#placeholder till real authentication is implemented
sig validAuth : (String, String) ~> String
fun validAuth(email, pass) server {
    var curentUser = email;
    var userid = 
      query {
        for (x <-- users)
          where (x.email == curentUser && x.password == pass)
            [(a=x.userid)]
      };
    if (userid == []) {
        ""
    } else {
      setCookie("currentUser",  intToString(hd(userid).a));
      intToString(hd(userid).a)
}}

sig view : (Model) ~> MvuHTML.HTML(Message)
fun view(model) {
  
  Common.viewHeader() +*
  form(a0,
    input(type("email") +@ placeholder("Email") +@ name("email") +@ onInput(fun(str) { UpdateEmail(str) }), h0) +*
    input(type("password") +@ placeholder("Password") +@ name("password") +@ onInput(fun(str) { UpdatePassword(str) }), h0) +*
    button(onClick(fun() { TryLogin }), textNode("Log In")) 
    )
    }

sig updt : (Message, Model) ~%~> (Model, MvuCommands.Command(Message))
fun updt(msg, model) {
  switch(msg) {
    case TryLogin ->
      var cmd =
        MvuCommands.spawnProc(fun() { DeliverResult(validAuth(model.email, model.password)) });
      ((model with loginSuccess = false), cmd)
    case DeliverResult(correct) -> 
      if (correct <> "") {
          redirect("/")
      } else {
        setCookie("currentUser", "")
      };
    ((model with loginSuccess = true), MvuCommands.empty)
    case UpdateEmail(txt) -> ((model with email = txt), MvuCommands.empty)
    case UpdatePassword(txt) -> ((model with password = txt), MvuCommands.empty)
  }
}


fun mainPage() {
    var loggedIn = getCookie("currentUser") <> "";
    if (loggedIn) {
        redirect("/")
    } else {
        ()
    };

    Mvu.runCmd("placeholder", initialModel, view, updt, MvuCommands.empty);
    Common.template()
}